{**************************************************************************************************}
{                                                                                                  }
{ Unit uWmiDelphiClass                                                                             }
{ Base class  for the classes generated by the Delphi Wmi Class generator                          }
{                                                                                                  }
{ The contents of this file are subject to the Mozilla Public License Version 1.1 (the "License"); }
{ you may not use this file except in compliance with the License. You may obtain a copy of the    }
{ License at http://www.mozilla.org/MPL/                                                           }
{                                                                                                  }
{ Software distributed under the License is distributed on an "AS IS" basis, WITHOUT WARRANTY OF   }
{ ANY KIND, either express or implied. See the License for the specific language governing rights  }
{ and limitations under the License.                                                               }
{                                                                                                  }
{ The Original Code is uWmiDelphiClass.pas.                                                        }
{                                                                                                  }
{ The Initial Developer of the Original Code is Rodrigo Ruz V.                                     }
{ Portions created by Rodrigo Ruz V. are Copyright (C) 2010 Rodrigo Ruz V.                         }
{ All Rights Reserved.                                                                             }
{                                                                                                  }

unit uWmiDelphiClass;

interface

uses
Classes;

type
  TWmiClass=class
   private
    FWmiServer: string;
    FWmiUser: string;
    FWmiPass: string;
    procedure DisposeCollection;
    procedure SetWmiServer(const Value: string);
    procedure SetWmiUser(const Value: string);
    procedure SetWmiPass(const Value: string);
   protected
    FWmiNameSpace            : string;
    FWmiClass                : string;
    FWMiDataLoaded           : Boolean;
    FWmiCollectionIndex      : Integer;
    FWmiCollection           : TList;
    FWmiIsLocal              : Boolean;
    function    LoadWmiData(const WmiNamespace,WmiClass:string): boolean;overload;
    constructor Create(LoadData:boolean;const WmiNamespace,WmiClass:string); overload;
    function    RunSimpleWmiMethod(const MethodName:string) : integer;
   public
    property  WmiServer : string read FWmiServer write SetWmiServer;
    property  WmiUser : string read FWmiUser write SetWmiUser;
    property  WmiPass: string read FWmiPass write SetWmiPass;
    function  GetPropertyValue(const PropName: string): OleVariant;
    function  GetCollectionCount:Integer;
    function  GetCollectionIndex:Integer;
    procedure SetCollectionIndex(Index: Integer);virtual;
    procedure LoadWmiData;overload;
    Destructor  Destroy; override;
  end;

  TWmiError=class
  private
    FOperation: string;
    FProviderName: string;
    FParameterInfo: string;
  public
    property Operation : string read FOperation;
    property ParameterInfo : string read FParameterInfo;
    property ProviderName : string read FProviderName;
    constructor Create; overload;
    Destructor  Destroy; override;
  end;


  function VarStrNull(const V:OleVariant):string;
  function VarByteNull(const V:OleVariant):Byte;
  function VarWordNull(const V:OleVariant):Word;
  function VarIntegerNull(const V:OleVariant):Integer;
  function VarInt64Null(const V:OleVariant):Int64;
  function VarLongNull(const V:OleVariant):Longint;
  function VarBoolNull(const V:OleVariant):Boolean;
  function VarDoubleNull(const V:OleVariant):Double;
  function VarDateTimeNull(const V : OleVariant): TDateTime; //UTC

implementation

Uses
 Variants,
 Activex,
 ComObj,
 SysUtils;


type
  TVariantClass=class
   Value : OleVariant;
  end;

Const
 DefaultDoubleNullValue =0.0;
 DefaultDateTimeNullValue =0;
 DefaultByteNullValue =0;
 DefaultWordNullValue =0;
 DefaultIntegerNullValue =0;
 DefaultInt64NullValue=0;
 DefaultLongNullValue =0;
 DefaultBoolNullValue =False;


function VarDoubleNull(const V:OleVariant):Double;
begin
  Result:=DefaultDoubleNullValue;
  if not VarIsNull(V) then
    Result:=V;
end;

function VarStrNull(const V:OleVariant):string;
begin
  Result:='';
  if VarIsArray(V) and not VarIsNull(V) then
    Result:= V[0]
  else
  if not VarIsNull(V) then
    Result:=VarToStr(V);
end;

function VarByteNull(const V:OleVariant):Byte;
begin
  Result:=DefaultByteNullValue;
  if not VarIsNull(V) then
    Result:=V;
end;


function VarWordNull(const V:OleVariant):Word;
begin
  Result:=DefaultWordNullValue;

  if VarIsArray(V) and not VarIsNull(V) then
    Result:= V[0]
  else
  if not VarIsNull(V) then
    Result:=  StrToInt(VarToStr(V));
end;

function VarIntegerNull(const V:OleVariant):Integer;
begin
  Result:=DefaultIntegerNullValue;
  if not VarIsNull(V) then
    Result:=V;
end;

function VarInt64Null(const V:OleVariant):Int64;
begin
  Result:=DefaultInt64NullValue;
  if not VarIsNull(V) then
    Result:=0;
end;

function VarLongNull(const V:OleVariant):Longint;
begin
  Result:=DefaultLongNullValue;
  if not VarIsNull(V) then
    Result:=V;
end;

function VarBoolNull(const V:OleVariant):Boolean;
begin
  Result:=DefaultBoolNullValue;
  if not VarIsNull(V) then
    Result:=V;
end;

//Universal Time (UTC) format of YYYYMMDDHHMMSS.MMMMMM(+-)OOO.
//20091231000000.000000+000
function VarDateTimeNull(const V : OleVariant): TDateTime;
var
 Year, Month, Day    : Word;
 Hour, Min, Sec, MSec: Word;
 UtcStr              : string;
begin
  Result:=0;
  UtcStr:=VarStrNull(V);
  if Length(UtcStr)>=15 then
  begin
     Year  :=StrToInt(Copy(UtcStr,1,4));
     Month :=StrToInt(Copy(UtcStr,5,2));
     Day   :=StrToInt(Copy(UtcStr,7,2));

     Hour  :=StrToInt(Copy(UtcStr,9,2));
     Min   :=StrToInt(Copy(UtcStr,11,2));
     Sec   :=StrToInt(Copy(UtcStr,13,2));
     MSec  :=0;
     Result:=EncodeDate(Year, Month, Day)+EncodeTime(Hour, Min, Sec, MSec);
  end;
end;

{
function VarDateTimeNull(const V:OleVariant):TDateTime;
begin
  Result:=DefaultDateTimeNullValue;
  if VarIsArray(V) and not VarIsNull(V) then
    Result:= V[0]
  else
  if not VarIsNull(V) then
    Result:=V;
end;
}
{ TWmiClass }
constructor TWmiClass.Create(LoadData:boolean;const WmiNamespace,WmiClass:string);
begin
  inherited Create;
  CoInitialize(nil);
  FWmiIsLocal         :=True;
  FWmiServer          :='localhost';
  FWmiUser            :='';
  FWmiPass            :='';
  FWMiDataLoaded      :=False;
  FWmiCollectionIndex :=-1;
  FWmiCollection:=TList.Create;
  FWmiNameSpace :=WmiNamespace;
  FWmiClass     :=WmiClass;
  if LoadData then
    FWMiDataLoaded:=LoadWmiData(WmiNamespace,WmiClass);
end;


destructor TWmiClass.Destroy;
begin
  DisposeCollection;
  FWmiCollection.Free;
  CoUninitialize;
  inherited;
end;

function TWmiClass.GetCollectionCount: Integer;
begin
 Result:=FWmiCollection.Count;
end;

function TWmiClass.GetCollectionIndex: Integer;
begin
 Result:=FWmiCollectionIndex;
end;


function TWmiClass.GetPropertyValue(const PropName: string): OleVariant;
var
 i  : integer;
begin
    i:=TStringList(FWmiCollection[FWmiCollectionIndex]).IndexOf(PropName);
    Result:=TVariantClass(TStringList(FWmiCollection[FWmiCollectionIndex]).Objects[i]).Value;
end;


//http://www.computerperformance.co.uk/Logon/code/code_80070005.htm#Local_Security_and_Policies_and_DCOM
function TWmiClass.LoadWmiData(const WmiNamespace,WmiClass:string): boolean;
var
  objSWbemLocator : OLEVariant;
  objWMIService   : OLEVariant;
  objWbemObjectSet: OLEVariant;
  oWmiObject      : OLEVariant;
  oEnum           : IEnumvariant;
  iValue          : LongWord;
  WmiProperties   : OLEVariant;
  oEnumProps      : IEnumvariant;
  PropItem        : OLEVariant;
  VariantObj      : TVariantClass;
begin;
 DisposeCollection;
 result:=True;
  try
    objSWbemLocator := CreateOleObject('WbemScripting.SWbemLocator');
    objWMIService   := objSWbemLocator.ConnectServer(FWmiServer, WmiNamespace, FWmiUser, FWmiPass);
    if not FWmiIsLocal then
      objWMIService.Security_.ImpersonationLevel := 3;

    objWbemObjectSet:= objWMIService.ExecQuery(Format('SELECT * FROM %s',[WmiClass]),'WQL',0);
    oEnum           := IUnknown(objWbemObjectSet._NewEnum) as IEnumVariant;
    while oEnum.Next(1, oWmiObject, iValue) = 0 do
    begin
      FWmiCollection.Add(TStringList.Create);
      WmiProperties := oWmiObject.Properties_;
      oEnumProps    := IUnknown(WmiProperties._NewEnum) as IEnumVariant;
        while oEnumProps.Next(1, PropItem, iValue) = 0 do
        begin
          VariantObj:=TVariantClass.Create;
          VariantObj.Value:=PropItem.Value;
          TStringList(FWmiCollection[FWmiCollection.Count-1]).AddObject(PropItem.Name,VariantObj);
          PropItem:=Unassigned;
        end;
     oWmiObject:=Unassigned;
    end;

    objSWbemLocator :=Unassigned;
    objWMIService   :=Unassigned;
    objWbemObjectSet:=Unassigned;
    oWmiObject      :=Unassigned;
    WmiProperties   :=Unassigned;
    PropItem        :=Unassigned;

    if FWmiCollection.Count>0 then
     SetCollectionIndex(0);

  except
   Result:=False;
  end;
end;

//executes a WMI method (no static) without in parameters,and with a single return value
function TWmiClass.RunSimpleWmiMethod(const MethodName: string): integer;
var
  objSWbemLocator : OLEVariant;
  objWMIService   : OLEVariant;
  objInvoker      : OLEVariant;
  ReturnValue     : OLEVariant;
  oEnum           : IEnumvariant;
  colItem         : OLEVariant;
  iValue          : LongWord;
  CurrIndex       : integer;
begin
 Result:=-1;
  objSWbemLocator := CreateOleObject('WbemScripting.SWbemLocator');
  objWMIService   := objSWbemLocator.ConnectServer('localhost',FWmiNamespace, '','');
  objInvoker      := objWMIService.InstancesOf(FWmiClass);
  oEnum           := IUnknown(objInvoker._NewEnum) as IEnumVariant;
  CurrIndex       := 0;
  while oEnum.Next(1, colItem, iValue) = 0 do
  begin
   if CurrIndex=FWmiCollectionIndex then
   begin
     ReturnValue:=colItem.ExecMethod(MethodName);//colItem.StopService;
     Result     :=VarIntegerNull(ReturnValue);
     Exit;
   end;
   Inc(CurrIndex);
  end;
end;


procedure TWmiClass.SetCollectionIndex(Index: Integer);
begin
  raise Exception.Create(Format('You must override this method %s',['SetCollectionIndex']));
end;

procedure TWmiClass.DisposeCollection;
var
 i : integer;
 j : integer;
begin
  for i:= 0 to FWmiCollection.Count - 1 do
  begin
    for j:= 0 to TStringList(FWmiCollection[i]).Count - 1 do
    begin
     TVariantClass(TStringList(FWmiCollection[i]).Objects[j]).Value:=Unassigned;
     TVariantClass(TStringList(FWmiCollection[i]).Objects[j]).Free;
    end;
   TStringList(FWmiCollection[i]).Free;
  end;
  FWmiCollection.Clear;
  FWmiCollectionIndex :=-1;
end;

procedure TWmiClass.SetWmiServer(const Value: string);
begin
  FWmiServer  := Value;
  FWmiIsLocal := false;
end;

procedure TWmiClass.SetWmiUser(const Value: string);
begin
  FWmiUser    := Value;
  FWmiIsLocal := false;
end;

procedure TWmiClass.SetWmiPass(const Value: string);
begin
  FWmiPass := Value;
  FWmiIsLocal := false;
end;

procedure TWmiClass.LoadWmiData;
begin
 LoadWmiData(FWmiNameSpace,FWmiClass);
end;

{ TWmiError }

constructor TWmiError.Create;
var
  objSWbemError :OleVariant;
begin
   inherited Create;
   CoInitialize(nil);
   objSWbemError  := CreateOleObject('WbemScripting.SWbemLastError');
   FOperation     :=VarStrNull(objSWbemError.Operation);
   FProviderName  :=VarStrNull(objSWbemError.ProviderName);
   FParameterInfo :=VarStrNull(objSWbemError.ParameterInfo);
end;

destructor TWmiError.Destroy;
begin
  CoUninitialize;
  inherited;
end;

end.
